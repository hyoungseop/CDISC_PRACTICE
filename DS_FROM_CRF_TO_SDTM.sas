

data DS00; set CRF.CRF_DS_SAMPLE; 
attrib
USUBJID format = $30.;

*1. DOMAIN 생성;
DOMAIN = 'DS';

*2. USUBJID 생성;
**STUDYID-SITEID-SUBJID;
if length(strip(SUBJID)) = 1 then USUBJID = catt(STUDYID,"-",SITEID,"-00",SUBJID);
else if length(strip(SUBJID)) = 2 then USUBJID = catt(STUDYID,"-",SITEID,"-0",SUBJID);
else USUBJID = catt(STUDYID,"-",SITEID,"-",SUBJID);

run;


proc sort data = DS00; by USUBJID; run;


data DS01; set DS00;

retain DSSEQ;

by USUBJID;

*3. DSSEQ 생성;
*ID당 SEQ;
if first.USUBJID then DSSEQ = 1;
else DSSEQ = DSSEQ + 1;


*4. DSSTDTC 생성;
DSSTDTC = put(DSSTDAT, e8601da.);
run;

proc freq data = DS01; table DSCAT DSTERM DSDECOD/nocol norow nopercent nocum; run;
*
DSCAT	Frequency
DISPOSITION EVENT	34

DSTERM	Frequency
COMPLETED	10
ENROLLED	17
WITHDRAWN	7
-> DSTERM(CRF 입력값 그대로)

DSDECOD	Frequency
COMPLETED	10
ENROLLED	17
WITHDRAWN	7
-> WITHDRAWN BY SUBJECT로 수정(명확한 원인 구분하기 위함)
**IDDO IG 기준 WITHDRAWAL이나 CDISC IG 기준(WITHDRAWN) 활용
;
data DS02; set DS01;

*5. DSDECOD WITHDRAWN 수정;
if DSDECOD = 'WITHDRAWN' then DSDECOD = 'WITHDRAWN BY SUBJECT';

run;

data SDTM.SDTM_DS_SAMPLE(keep = STUDYID DOMAIN USUBJID DSSEQ DSTERM DSDECOD DSCAT DSSTDTC);
retain STUDYID DOMAIN USUBJID DSSEQ DSTERM DSDECOD DSCAT DSSTDTC;
set DS02;
run;